// Описание: 
// 1. Удаляем крайние нули (01,02,03)
// 2. Убираем по единицы слева и справа (04,06,07,11)
// 3. Если нельзя удалить справа - дописываем слева ноль 
//    и заканчиваем (05)
// 4. Если нельзя удалить слева - дописываем справа единицу,
//    которую уже взяли, и ноль и заканчиваем (08,09,10)

// 01: ([0] 1^m 0 1^n 0)
01 0 -> 02 " R

// 02: ([1^m 0 1^n 0])
02 1 -> 02 1 R
02 0 -> 02 0 R
02 " -> 03 " L
 
// 03: (1^m 0 [1^n 0])
03 0 -> 04 " L

// 04: (1^m [0 1^n])
04 0 -> 05 0 L	// n == 0
04 1 -> 06 " L	// n <> 0

// 05: n == 0, ([" 1^m] 0)
05 1 -> 05 1 L
05 " ->  . 0 .	// .: ([0] 1^m 0)

// 06: n <> 0, ([" 1^m 0 1^(n-1)])
06 0 -> 06 0 L
06 1 -> 06 1 L
06 " -> 07 " R

// 07: n <> 0, ([1^m 0] 1^(n-1))
07 0 -> 08 0 R	// m == 0
07 1 -> 11 " R	// m <> 0 -> m := m-1, n := n-1

// 08: n <> 0, m == 0, (0 [1^(n-1)])
08 1 -> 08 1 R
08 " -> 09 1 R

// 09: n <> 0, m == 0, (0 1^n ["])
09 " -> 10 0 L

// 10: n <> 0, m == 0, ([0 1^n] 0)
10 1 -> 10 1 L
10 0 ->  . 0 .	// .: ([0] 1^n 0)

// 11: ([1^m 0 1^n "])
11 1 -> 11 1 R
11 0 -> 11 0 R
11 " -> 04 " L
